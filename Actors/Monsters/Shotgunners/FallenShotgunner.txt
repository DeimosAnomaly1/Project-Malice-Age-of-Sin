Class PM_FallenShotgunner : PMUnaffMonsterBase
{
   Default
   {
   PMMonsterBase.CodexLore 
   "Each engagement against the enemy was tougher than the last. Not only did they have to deal with steadily decreasing logistics and manpower, they also had to deal with treating those that were especially traumatized after repeat conflicts.
    For some, the mere idea of evil itself taking upon physical and spiritual form was impossible to comprehend, and sooner or later their minds would fall to insanity after prolonged exposure, the ambient evil around passively influencing them.
	When the main resistance crumbled, more of this phenomenon would occur, and it didn't take long for almost the entirety of humanity to fall into the clutches of evil.
	
	Being an elite, this lunatic attacks you with its powerful shotgun, but can also deploy a smoke grenade and retreat to cover to reload.";
   Health 80;
   Height 56;
   Radius 20;
   Speed 8;
   Mass 100;
   PainChance 96;
   SeeSound "FallenSoldiers/Sight";
   ActiveSound "FallenSoldiers/Active";
   PainSound "FallenSoldiers/Pain";
   DeathSound "FallenSoldiers/Death";
   Obituary "%o's innards were revealed by a Fallen Shotgunner.";
   DropItem "Shotgun";
   //DropItem "Lith_Shell";
   Species "Shotgunner";
   Tag "Fallen Shotgunner";
   Bloodtype "PM_NormalBloodBase";
   TeleFogSourceType "PM_PossessedTeleportFog";
   TeleFogDestType "PM_PossessedTeleportFog";
   Monster;
    +FloorClip
    +NoTarget
	+MissileMore
	+HARMFRIENDS
	+DOHARMSPECIES
   }
  States
  {
  Spawn:
    ZSP1 A 0 NoDelay
    {
     user_numbullets = random(5,8);
    }
  Idle:
        ZSP1 AAAAAAAAAA 10 PM_Look();
		ZSP1 A 0
		{
		if (!random(0, 30))
			{
				A_StartSound(activesound,554,volume:frandom(0.1,0.5),pitch:frandom(0.75,0.88));
			}
		}
		ZSP1 A 0 A_Jump(45,"Explore");
		Loop;
	Explore:
		TNT1 A 0 A_SetSpeed(6);
		TNT1 A 0
		{
		if (!random(0, 12))
			{
				A_StartSound(activesound,554,volume:frandom(0.1,0.5),pitch:frandom(0.75,0.88));
			}
		}
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",4,volume:0.5);
		ZSP1 AAABBB 4
		{
		PM_Look();
		A_Wander();
		}
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",4,volume:0.5);
		ZSP1 CCCDDD 4
		{
		A_Wander();
		PM_Look();
		}
		ZSP1 A 0 A_Jump(40,"Idle");
		Loop;
  Look:
    TNT1 A 0 A_Jump(15,"Spotted");
	TNT1 A 0 A_SetSpeed(7);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    ZSP1 AAA 2 PM_LookingForPlayer();
	ZSP1 BBB 2 PM_LookingForPlayer();
    TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	ZSP1 CCC 2 PM_LookingForPlayer();
	ZSP1 DDD 2 PM_LookingForPlayer();
    Goto Look+1;
  Spotted:
    TNT1 A 0 A_Jump(256,"Roar","SpotMissile","See");
	Goto See;
  Roar:
    TNT1 A 0 PM_AlertSound();
	Goto See;
  SpotMissile:
    TNT1 A 0 PM_AlertSound();
    ZSP1 EEE 5 A_FaceTarget();
	Goto Missile;
  See:
	TNT1 A 0 A_Jump(35,"Backup");
	TNT1 A 0 A_SetSpeed(7);
    TNT1 A 0 { bFRIGHTENED = false; }
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    ZSP1 AAA 2 A_Chase();
	ZSP1 BBB 2 A_Chase();
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	ZSP1 CCC 2 A_Chase();
	ZSP1 DDD 2 A_Chase();
    Loop;
  Missile:
    TNT1 A 0
	{
	if(user_numbullets < 0)
	{
	 return ResolveState("NoAmmo");
	}
	return ResolveState(null);
	}
    TNT1 A 0 A_CheckRange(300,"SmokeShieldCheck");
    TNT1 A 0 A_Jump(55,"RealMissile2");
  RealMissile:
	ZSP1 EEE 4
		{
		A_FaceTarget(360,180);
		A_SetAngle(angle+random(-5,5));
		A_SetPitch(pitch+random(-5,5));
		}
    ZSP1 F 2 Bright Light("YellowMediumFlicker2")
    {
	  A_StartSound("ShotgunnerGunDistant",CHAN_7,CHANF_OVERLAP);
      A_StartSound("FSGunner/Fire", CHAN_WEAPON);
	  A_SpawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
	  A_SpawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
	  A_SPawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
	  A_SPawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
      user_numbullets--;
    }
    ZSP1 EEE 5 A_FaceTarget(5,5);
	ZSP1 A 0 A_StartSound("GenericShotgunPump");
	ZSP1 AA 5 A_FaceTarget(5,5);
    TNT1 A 0 A_SpawnItemEx("PM_ShellCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
	ZSP1 BB 5 A_FaceTarget(5,5);
    Goto See;
  RealMissile2:
    TNT1 A 0 A_StartSound("FallenSoldiers/Sight",CHAN_5);
	ZSP1 E 9
		{
		A_FaceTarget(360,180);
		A_SetAngle(angle+random(-5,5));
		A_SetPitch(pitch+random(-5,5));
		}
    ZSP1 F 2 Bright Light("YellowMediumFlicker2")
    {
	  if(user_numbullets < 0)
	  {
	   return ResolveState("NoAmmo");
	  }
	  A_StartSound("ShotgunnerGunDistant",CHAN_7,CHANF_OVERLAP);
      A_StartSound("FSGunner/Fire", CHAN_WEAPON);
	  A_SpawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
	  A_SpawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
	  A_SPawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
	  A_SPawnProjectile("PM_BulletTracer",35,7,frandom(-2, 2),CMF_AIMDIRECTION,pitch+frandom(-3,3));
      user_numbullets--;
	  return ResolveState(null);
    }
    ZSP1 EEE 3 A_FaceTarget(5,5);
	ZSP1 A 0 A_StartSound("GenericShotgunPump");
	ZSP1 AA 4 A_FaceTarget(5,5);
    TNT1 A 0 A_SpawnItemEx("PM_ShellCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
	ZSP1 BB 4 A_FaceTarget(5,5);
	TNT1 A 0 A_Jump(30,"See");
	TNT1 A 0 A_MonsterRefire(60,"See");
    Goto RealMissile2+1;
  SmokeShieldCheck:
    TNT1 A 0 A_JumpIfInventory("FallenSGunnerSmokeToken",1,"RealMissile");
	TNT1 A 0 A_CheckProximity("RealMissile","PM_FallenSGunnerSmokeGrenade",800);
	TNT1 A 0 A_CheckProximity("RealMissile","PM_FallenShotgunSmoke",800);
    TNT1 A 0 A_CheckProximity("SmokeShield","PMMonsterBase",600,4,CPXF_ANCESTOR|CPXF_CHECKSIGHT);
	Goto RealMissile;
  SmokeShield:
	TNT1 A 0;
	TNT1 A 0 A_Jump(130,"RealMissile");
    ZSP1 EEE 4 A_FaceTarget();
	TNT1 A 0 A_CheckProximity("RealMissile","PM_FallenSGunnerSmokeGrenade",800);
	TNT1 A 0 A_CheckProximity("RealMissile","PM_FallenShotgunSmoke",800);
	TNT1 A 0 A_GiveInventory("FallenSGunnerSmokeToken",1);
    ZSP1 F 2 Bright Light("YellowMediumFlicker2")
    {
	  A_StartSound("ShotgunnerGunDistant",CHAN_7,CHANF_OVERLAP);
      A_StartSound("GrenadeLaunch", CHAN_WEAPON);
      A_FaceTarget();
	  A_SpawnProjectile("BulletFlare", 35, 15, 15, 0);
	  A_SpawnProjectile("BulletFlare", 35, 15, 15, 0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnProjectile("PM_FallenSGunnerSmokeGrenade",33,0,random(-1,1),CMF_OFFSETPITCH,-10);
    }
    ZSP1 EE 5 A_FaceTarget();
	ZSP1 A 0 A_StartSound("GenericShotgunPump");
	ZSP1 AA 4 A_FaceTarget();
	ZSP1 BB 4 A_FaceTarget();
    Goto Backup;
  Backup:
		TNT1 A 0 A_CheckSight("See");
		ZSP1 DCBADCBADCBA 3
		{
		A_FaceTarget();
		A_Recoil(2);
		A_FaceTarget();
		}
	Goto See;
  Retreat:
    TNT1 A 0 { bFRIGHTENED = true; }
	TNT1 A 0 A_SetSpeed(10);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    ZSP1 AAA 2 A_Chase();
	ZSP1 BBB 2 A_Chase();
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	ZSP1 CCC 2 A_Chase();
	ZSP1 DDD 2 A_Chase();
	TNT1 A 0 A_Jump(20,"ZReload");
    TNT1 A 0 A_CheckSight("ZReload");
    Goto Retreat;
  NoAmmo:
		ZSP1 E 7 A_FaceTarget();
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		ZSP1 EE 10 A_FaceTarget();
		TNT1 A 0 A_Jump(100,"Retreat");
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		ZSP1 E 8 A_FaceTarget();
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		ZSP1 E 8 A_FaceTarget();
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		ZSP1 AAAA 5 A_FaceTarget();
		Goto Retreat;
  ZReload:
    TNT1 A 0;
	TNT1 A 0 A_FaceTarget();
    TNT1 A 0 
	{
	if(user_numbullets == 8)
	SetStateLabel("ZReload2");
	}
    ZSP1 AA 3;
    TNT1 A 0 A_StartSound("PM/ShellInsert",CHAN_5);
    TNT1 A 0 { user_numbullets += 1; }
    ZSP1 BB 2;
    ZSP1 AAA 3;
	TNT1 A 0
	{
	 if(target && CheckSight(Target) && Distance3D(target) < 250)
	 SetStateLabel("ZReload2");
	}
    Loop;
  ZReload2:
     TNT1 A 0;
	 TNT1 A 0 A_StartSound("GenericShotgunPump");
	 ZSP1 AA 5 A_FaceTarget();
	 ZSP1 BB 5 A_FaceTarget();
     Goto See;
  Pain:
    ZSP1 G 3 A_Pain;
    ZSP1 G 5;
    Goto See;
  Death:
    ZSP1 H 5;
    ZSP1 I 5 PM_Scream();
    ZSP1 J 5;
    ZSP1 K 5 A_NoBlocking;
	TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
    ZSP1 L -1;
	Stop;
  XDeath:
    TNT1 A 0 A_StopSound(555);
	  ZSP1 H 5 PM_Gib();
    ZSP1 I 5 A_Scream;
    ZSP1 J 5 A_NoBlocking;
    ZSP1 K 5;
	TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
    ZSP1 L -1;
	Stop;
  Raise:
    ZSP1 LKJIH 5;
    Goto Look;
  }
}

Class PM_FallenSGunnerSmokeGrenade : Rocket
{
  int user_grenadetime;
  int user_smoketime;
  Default
  {
  Speed 16;
  Scale 0.66;
  Radius 4;
  Height 10;
  Gravity 0.8;
  Bouncetype "Hexen";
  BounceFactor 0.44;
  WallBounceFactor 0.66;
  BounceCount 5;
  SeeSound "";
  DeathSound "";
  BounceSound "ZTrooper/FragBounce";
  Decal "";
  -NOGRAVITY
  -ROCKETTRAIL
  +THRUACTORS
  +ROLLSPRITE
  +CANBOUNCEWATER
  +BOUNCEONCEILINGS
  }
  States
  {
  Spawn:
	GRNP A 1 
    {
     A_SpawnItemEx("PM_DevilBallSmoke",random(-1,1),random(-1,1),0,0,0,0,0,128,50);
    }
	Loop;
  Death:
	GRNP AA 1
		{
		if (user_grenadetime >= 10)
			{
			return ResolveState("Smoke");
			}
		//A_SpawnItemEx("PM_FallenShotgunSmoke",0,frandom(-2,2),frandom(-2,2),random(-5,5),random(-5,5),random(0,3));
		user_grenadetime++;
		return ResolveState(null);
		}
	Loop;
  Smoke:
	TNT1 A 0 A_StartSound("SmokeGrenadeExp",CHAN_7);
  Smoke1:
    GRNP AA 1
	{
		if (user_smoketime >= 16)
			{
			return ResolveState("XDeath");
			}
		A_SpawnItemEx("PM_FallenShotgunSmoke",0,frandom(-2,2),frandom(-2,2),random(-6,6),random(-6,6),random(0,2));
		user_smoketime++;
		return ResolveState(null);
    }
	Loop;
   XDeath:
    GRNP A -1;
    Stop;
	}
}

Class FallenSGunnerSmokeToken : Inventory 
{ 
 Default
 {
  +Inventory.undroppable;
  Inventory.maxamount 2;
 }
}

Class PM_FallenShotgunSmoke : Actor
{
  Default
  {
  Radius 2;
  Height 2;
  Mass 0x7fffffff;
  +NOBLOCKMAP
  +NOGRAVITY
  +DROPOFF
  +NODAMAGETHRUST
  +DONTSPLASH
  +NOTELEPORT
  +SKYEXPLODE
  +FORCEXYBILLBOARD
  +FOILINVUL
  +CANBLAST
  +RIPPER
  +THRUACTORS
  +BLOODLESSIMPACT
  +BLOCKEDBYSOLIDACTORS
  +FORCERADIUSDMG
  +DOOMBOUNCE
  +ROLLSPRITE
  BounceFactor 0.3;
  WallBounceFactor 0.3;
  RenderStyle "Translucent";
  Alpha 0.86;
  Scale 2.5;
  DeathSound "RevGhoul/cloud";
  }
  states
  {
  Spawn:
	TNT1 A 1;
	TNT1 A 0 A_SetRoll(random(0,360));
	TNT1 A 0 A_SetScale(0.1);
	GNSM KKKKKKKKKKKKKKK 2 A_SetScale(scale.x+0.07);
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	GNSM K 600;
	GNSM KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 8
	{
	A_FadeOut(0.04);
	}
  Death:
	TNT1 A 1;
	Stop;
  }
}