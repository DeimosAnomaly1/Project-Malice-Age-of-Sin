Class PM_Squire : PMMonsterBase
{
	int user_leapstrength;
	Default
	{
	PMMonsterBase.CodexLore
	"Squires are the common berserkers within Hell's endless legion, renown for their unrestrained aggression and forgoing fire and flames in favor of simply brutalizing humans left and right, their animalistic behavior yet another method of inducing fear into opposing soldiers and survivors.
	 They are part of the same bloodline as Heresy Lords and Nobles, but are ranked far lower in the same level as Devils and Wretched because of their intelligence, or lack thereof.

	 They are fast, brutal and WILL close in on you, but like the standard Hellknights, are no match for a shotgun blast to the face or two.";
	Health 500;
	Radius 24;
	Height 64;
	Mass 1000;
	Speed 7;
	PainChance 50;
	MeleeRange 75;
	Monster;
	Bloodtype "PM_BlackBloodBase";
	Bloodcolor "Black";
	+FLOORCLIP
	+DONTHARMSPECIES
	Species "Nobles";
	Tag "Squire";
	SeeSound 	"Squire/Sight";
	ActiveSound "Squire/Active";
	PainSound 	"Squire/Pain";
	DeathSound 	"Squire/Death";
	HitObituary "%o was punched out by a Squire.";
	Obituary "%o was dispersed by a Squire.";
	Scale 1.05;
	Gravity 0.83;
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	States
	{
	Spawn:
		BOS6 AAAAAAAAAA 10 PM_Look();
		BOS6 A 0
		{
		if (!random(0, 30))
			{
				A_StartSound(activesound,554,volume:frandom(0.05,0.3),pitch:frandom(0.60,0.78));
			}
		}
		BOS6 A 0 A_Jump(45,"Explore");
		Loop;
	Explore:
		TNT1 A 0
		{
		if (!random(0, 12))
			{
				A_StartSound(activesound,554,volume:frandom(0.05,0.3),pitch:frandom(0.60,0.78));
			}
		}
		TNT1 A 0 A_SetSpeed(4);
		TNT1 A 0 A_StartSound("Squire/Step",4,volume:0.35);
		BOS6 AAAABBBB 4
		{
		PM_Look();
		A_Wander();
		}
		TNT1 A 0 A_StartSound("Squire/Step",4,volume:0.35);
		BOS6 CCCCDDDD 4
		{
		A_Wander();
		PM_Look();
		}
		BOS6 A 0 A_Jump(40,"Spawn");
		Loop;
	Look:
		TNT1 A 0 A_SetSpeed(5);
		BOS6 AAAABBBB 2 PM_LookingForPlayer();
		TNT1 A 0 A_StartSound("Squire/Step");
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",180,JLOSF_DEADNOJUMP);
		BOS6 CCCCDDDD 2 PM_LookingForPlayer();
		TNT1 A 0 A_StartSound("Squire/Step");
        Loop;
    Spotted:
        Goto Roar;
	See:
	    TNT1 A 0 { bSPRITEFLIP = false; }
		TNT1 A 0 A_Jump(20,"See2");
		TNT1 A 0 A_SetSpeed(4);
		BOS6 AAAA 2 A_Chase();
		BOS6 BBBBBB 1 A_Chase();
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCC 2 A_Chase();
		BOS6 DDDDDD 1 A_Chase();
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		Loop;
	See2:
	    TNT1 A 0 { bSPRITEFLIP = false; }
		TNT1 A 0 A_SetSpeed(12);
		BOS6 AA 3 A_Chase();
		TNT1 AA 0 A_Recoil(-0.8);
		BOS6 BB 3 A_Chase();
		TNT1 A 0 A_StartSound("Squire/Step");
		TNT1 AA 0 A_Recoil(-1.5);
		BOS6 CC 3 A_Chase();
		TNT1 AA 0 A_Recoil(-0.8);
		BOS6 DD 3 A_Chase();
		TNT1 A 0 A_StartSound("Squire/Step");
		TNT1 AA 0 A_Recoil(-1.5);
		TNT1 A 0 A_Jump(5,"See2");
		Loop;
	SeeNoMissile:		
		BOS6 G 20;
		BOS6 AAAA 2 A_Chase("Melee","Fireball");
		BOS6 BBBBBB 1 A_Chase("Melee","Fireball");
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCC 2 A_Chase("Melee","Fireball");
		BOS6 DDDDDD 1 A_Chase("Melee","Fireball");
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 AAAA 2 A_Chase("Melee","Fireball");
		BOS6 BBBBBB 1 A_Chase("Melee","Fireball");
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCC 2 A_Chase("Melee","Fireball");
		BOS6 DDDDDD 1 A_Chase("Melee","Fireball");
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 AAAA 2 A_Chase("Melee","Fireball");
		BOS6 BBBBBB 1 A_Chase("Melee","Fireball");
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCC 2 A_Chase("Melee","Fireball");
		BOS6 DDDDDD 1 A_Chase("Melee","Fireball");
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		Goto See;	
	Melee:
		TNT1 A 0 A_Jump(128,"MeleeLeap");
		BOS6 E 6 A_FaceTarget();
		BOS6 E 0 A_StartSound("Doom4/Other/MeleeSwing",22,CHANF_OVERLAP);
		BOS6 E 4 { A_Recoil(-2); A_FaceTarget(360,180); }
		BOS6 F 2 A_Recoil(-4);
		BOS6 G 4		
		{
		  for(int i=3;i>0;i--)
		   { A_SpawnProjectile("PM_MeleeHitbox",flags:CMF_AIMDIRECTION,pitch); }
		}
		TNT1 A 0 { bSPRITEFLIP = true; }
		BOS6 E 6 A_FaceTarget();
		BOS6 E 0 A_StartSound("Doom4/Other/MeleeSwing",22,CHANF_OVERLAP);
		BOS6 E 4 { A_Recoil(-2); A_FaceTarget(360,180); }
		BOS6 F 2 A_Recoil(-4);
		BOS6 G 4 
		{
		  for(int i=3;i>0;i--)
		   { A_SpawnProjectile("PM_MeleeHitbox",flags:CMF_AIMDIRECTION,pitch); }
		}
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIfCloser(290,"DecideToCharge");
		TNT1 A 0 A_Jump(100, "Leap");
		TNT1 A 0 A_Jump(230,"FireBall");
		Goto Leap;
	DecideToCharge:
	    TNT1 A 0 A_JumpIfHigherOrLower("Fireball",null);
		BOS6 M 0 A_Jump(25,"ChargeAttack");
		Goto Missile+1;
	Roar:
		BOS6 PQR 2 A_FaceTarget();
		TNT1 A 0 PM_AlertSound();
		BOS6 STSUSTSU 3 A_FaceTarget();
		BOS6 RQP 2 A_FaceTarget();
		TNT1 A 0 A_Jump(150,"See");
		Goto See2;
	PainRoar:
		BOS6 PQR 2 A_FaceTarget();
		TNT1 A 0 A_StartSound("Squire/Sight",CHAN_6);
		BOS6 STSUSTSU 3 A_FaceTarget();
		BOS6 RQP 2 A_FaceTarget();
		TNT1 A 0 A_Jump(60,"See");
		Goto See2;
	FireBall:
		TNT1 A 0 A_Jump(30,"Roar");
		BOS6 EEEEEEEEE 1 Light("Greenball4") 
		{
		A_FaceTarget();
		A_Spawnitemex("PM_GreenFireEffect",10,random(30,25),60,0,0,2);
		}
		BOS6 EEEE 1 Light("Greenball4")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_GreenFireEffect",10,random(30,25),57,0,0,2);
		}
		BOS6 FFFF 1 Light("Greenball3") 
		{
		A_FaceTarget();
		A_Spawnitemex("PM_GreenFireEffect",10,random(30,25),45,0,0,2);
		}
		BOS6 G 6 Light("Greenball3")
		{
		A_FaceTarget();
		A_StartSound("HellKnight/Cast",CHAN_7);
		A_SpawnProjectile("PM_SquireFireball",40);
		}
		BOS6 G 5 Light("Greenball3")
		{
		A_FaceTarget();
		}
		Goto See;
	MeleeLeap:
		BOS6 EE 2  A_FaceTarget();
		BOS6 E 3 A_FaceTarget();
		TNT1 A 0 A_StartSound("Squire/Leap",13);
		TNT1 A 0 A_Recoil(-3);
		TNT1 A 0 ThrustThingZ(0,35,0,1);
		Goto LeapLoop;
	Leap:
		TNT1 A 0 A_Jump(55,"Roar");
	    TNT1 A 0 
	    { 
	     if(target && Distance3D(target) >= 568)
	      { SetStateLabel("Fireball"); }
	    }
		TNT1 A 0 A_JumpIfCloser(256,"ChargeAttack"); //if too close, charges towards target
		TNT1 A 0 A_StartSound("Squire/LeapReady",13);
		BOS6 EEEEE 4  A_FaceTarget();
		BOS6 EEE 5 A_FaceTarget();
		BOS6 E 2 PM_LeapTo(target, 568, "Fireball");
		TNT1 A 0 A_StartSound("Squire/Leap",13);
	LeapLoop:
		BOS6 FFFF 1 
		{
		 if(random(1,15) == 1)
		  { ThrustThing(random(0,360)*256/360,2,0,0); }
		  A_SpawnItemEx("KnightLeapTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
        TNT1 A 0 A_CheckFloor("LeapEnd");
		Loop;
		LeapEnd:
		 TNT1 A 0 A_StartSound("Doom4/Monsters/BaronFloorPound",CHAN_7,CHANF_OVERLAP);
		 TNT1 A 0 A_Quake(4,20,0,512);
         TNT1 AAAAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareEffectLong",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
         TNT1 A 0
		 {
		  for (int i = 0; i < 360; i += random(10, 30))
		   {
			A_SpawnParticleEx("", TexMan.CheckForTexture("GNSMK0"), STYLE_TRANSLUCENT, SPF_RELATIVE, random(90, 95), frandom(56,100), i, zoff: 5, velx: frandom(-5,5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), accelz: 0.01, startalphaf: 0.35, fadestepf: 0.0075, sizestep: 2);
			A_SpawnParticleEx("", TexMan.CheckForTexture("GNSML0"), STYLE_ADD, SPF_RELATIVE, random(35, 65), frandom(35,75), i, zoff: 5, velx: frandom(-5,5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), accelz: 0.01, startalphaf: 0.65, fadestepf: 0.0075, sizestep: 2);
		   }
	    for (int i = random(10, 24); i > 0; i--)
		A_SpawnParticleEx("", TexMan.CheckForTexture("DUSTC0"), STYLE_NORMAL, SPF_ROLL||SPF_RELATIVE, random(90, 95), random(5,20), 0, frandom(-20, 20), frandom(-20, 20), 10, velx: frandom(-9, 9), vely: frandom(-9, 9), velz: frandom(1, 20), accelz: -0.5, sizestep: -0.05, startroll:frandom(-180,180));
		 }
         TNT1 AA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		 TNT1 A 0 
		 {
          A_QuakeEx(2, 2, 2, 35, 0, 300, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 500);
		  A_Explode(25, 190, XF_NOTMISSILE, damagetype: "Nobles");
	     }
		 BOS6 G 1 A_Stop;
		 BOS6 G 2;
		 Goto SeeNoMissile; 
	ChargeAttack: 
	   TNT1 A 0 A_Jump(60,"Roar");
	   TNT1 A 0 { bNOTARGETSWITCH = true; }
	   TNT1 A 0 A_StartSound("Squire/Charge",13);
	   BOS6 EFF 4 A_FaceTarget();
	   TNT1 A 0 ThrustThingZ(0,1,0,1);
	   BOS6 AAABBBCCCDDD 2
	   {
	     if(target && Distance3D(target) <= 64)
	     { SetStateLabel("ChargeAttackMelee"); }
	     A_Recoil(-5);
		 A_SpawnProjectile("PM_WeakMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
	   }
    ChargeAttackMelee:
	   TNT1 A 0 A_StartSound("Doom4/Other/MeleeSwing",22);
	   TNT1 A 0 { bSLIDESONWALLS = true; }
	   TNT1 A 0 ThrustThingZ(0,20,0,1);
	   BOS6 F 5 A_FaceTarget();
	   TNT1 A 0 A_ChangeVelocity(x:5,flags:CVF_RELATIVE);
	   BOS6 F 3 A_SpawnProjectile("PM_SquireLunge",18,0,0,CMF_AIMDIRECTION);
	   BOS6 G 6;
	   BOS6 F 2 A_FaceTarget();
	   BOS6 F 3 A_SpawnProjectile("PM_FastMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
	   BOS6 G 6;
	   BOS6 G 2 A_ScaleVelocity(0.2);
	   TNT1 A 0 { bSLIDESONWALLS = false; }
	   TNT1 A 0 { bNOTARGETSWITCH = false; }
	   BOS6 G 1 A_ScaleVelocity(0);  
	   Goto See;
	Pain:
		BOS6 H 2;
		BOS6 H 2 A_Pain;
		TNT1 A 0 { bSPRITEFLIP = false; }
		TNT1 A 0 A_Jump(20,"PainRoar");
		Goto See;
	Death:
	    TNT1 A 0 { bSPRITEFLIP = false; }
		BOS6 I 8 A_SpawnItemEx("PM_BlackMeatChunkLauncher",random(-2,2),random(-2,2),random(20,50),0,0,0,0,SXF_NOCHECKPOSITION);
		BOS6 J 8 PM_Scream();
		BOS6 K 8
		{
		A_SpawnItemEx("PM_BlackInstestinGib",random(-4,4),random(-4,4),random(25,35),random(-3,3),random(-3,3),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_BlackInstestinGib",random(-4,4),random(-4,4),random(25,35),random(-4,4),random(-4,4),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		}
		BOS6 L 8
		{
		A_NoBlocking();
		A_SpawnItemEx("PM_BlackInstestinGib",random(-4,4),random(-4,4),random(25,35),random(-4,4),random(-4,4),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		}
		TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
		BOS6 MN 8 A_SpawnItemEx("PM_BlackMeatChunkLauncher",random(-2,2),random(-2,2),random(10,30),0,0,0,0,SXF_NOCHECKPOSITION);
		BOS6 O -1;
		Stop;
	Raise:
		BOS6 O 8;
		BOS6 NMLKJI  8;
		Goto Look;
		
	}
}

Class KnightLeapTrail : Actor
{
   Default
   {
	+NOINTERACTION
	+CLIENTSIDEONLY
	Renderstyle "Translucent";
	Alpha 0.25;
    Scale 1.05;
   }
	States
	{
	Spawn:
		BOS6 F 2 A_FadeOut(0.05);
		Wait;
	}
}


Class PM_SquireLunge : Actor
{
   Default
   {
	Height 16;
	Radius 16;
	Speed 27;
	DamageFunction (5*Random(4,8));
	DamageType "Melee";
	Species "Fiends";
	SeeSound "";
	DeathSound "Doom4/Other/MeleeHit";
	PROJECTILE;
   }
	States
	{
	Spawn:
		TNT1 A 4;
		Stop;
    }
}

Class PM_SquireFireball : PM_ImpBallBase
{
    Default
    {
	+NOGRAVITY
	-LOWGRAVITY
	Speed 15;
	SeeSound "HellKnight/Cast";
	DeathSound "HellKnight/BallImpact";
	Decal "PM_GreenFireScorch";
	DamageFunction 32;
	DamageType "Fire";
	Scale 0.24;
    }
	states
  	{
  Spawn:
    PBAL C 1 bright Light("GreenFireLight") 
	{
	A_SpawnItemEx("PM_GreenFlameTrails",0,0,0,0,0,0,0,128,0);
	A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
    A_SpawnParticleEx("",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 30,size: 30.0,xoff: -10,velx: frandom(-0.3,0.3),vely: frandom(-0.3,0.3),velz: frandom(-0.3,0.3),startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	A_SpawnParticleEx("", TexMan.CheckForTexture("DTPRA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 1, size: 85.0, xoff: -10, startalphaf: 0.5, startroll: random(-180,180));
	}
	loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_SetTranslation("PM_GreenFusionTint");
	TNT1 A 0 A_SetScale(1.2);
    NDEB D 1 bright Light("GreenFireLight");
	TNT1 A 0 A_SpawnItemEx("PM_GFireBallExplode",0,0,0,random(-4,4),random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_GreenFireballExpFlames",0,0,0,random(-4,4),random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION);
    FIR3 CCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 Light("GreenFireLight") bright A_FadeOut(0.05);
    stop;
  }
}

Class PM_GFireBallExplode : Actor
{
   Default
   {
	Scale 1;
	Alpha 0.84;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	Speed 1;
   }
	States
{
Spawn:
	FXPG AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.06);
	Stop;
	}
}